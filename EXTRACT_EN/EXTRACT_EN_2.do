******************************************************************************************************************************************************
* EXPOSE - DATA EXTRACTION & CONSOLIDATION                                                                                                           *
*                                                                                                                                                    *
* INPUT: HSE1998-2017.dta harmonised dataset generated by the SPSS code EXTRACT_EN_1.SPS                                                             *
* OUTPUT: Consolidated, harmonised EXPOSE ENGLAND DATASET (EXPOSE_EN.dta)                                                                            *
*                                                                                                                                                    *
* Kafui Adjaye-Gbewonyo (k.adjayegbewonyo@greenwich.ac.uk)                                                                                           *
* Version 1.2                                                                                                                                        *
******************************************************************************************************************************************************

clear
set more off

******************************************************************************************************************************************************
* LOCATION OF FILES AND FOLDERS                                                                                                                      * 
******************************************************************************************************************************************************

* BASE DATA DIRECTORY 
global BASEDIR "****************"      // Insert here the path of the base directory which containg the HSE1998-2017.dta dataset 

* OUTPUT DIRECTORY
global OUT "./OUT"

******************************************************************************************************************************************************
* LOAD DATA                                                                                                                                          * 
******************************************************************************************************************************************************

use "$BASEDIR/HSE1998-2017.dta", clear   

******************************************************************************************************************************************************
* DEFINE LABELS                                                                                                                                      * 
******************************************************************************************************************************************************

label drop _all

label define vyesno 0 "No" 1 "Yes"
label define vsmokstatus 0 "Never smoker" 1 "Former smoker" 2 "Current smoker"
label define vsex 1 "Male" 2 "Female"
label define vcurrpreg 0 "No/don't know" 1 "Yes" 
label define vrace_e 1 "Black" 2 "Mixed" 3 "White" 4 "Asian" 9999 "Other"
label define vrace_e2 1 "White" 2 "Black" 3 "Asian" 4 "Mixed/Other"

*label define vself_health_e 1 "Bad/very bad" 2 "Fair" 3 "Good" 4 "Very good"
label define vself_health 1 "Poor/bad" 2 "Average/fair" 3 "Good" 4 "Very good/excellent"
*label define vgeotype2_e   1 "Urban"  2 "Non-urban" 
label define vgeotype2   1 "Urban"  2 "Non-urban" 
label define vedu_e 0 "No qualification" 1 "NVQ1/CSE other grade equiv" 2  "NVQ2/GCE O Level equiv" 3 "NVQ3/GCE A Level equiv"  ///
                    4 " Higher ed below degree" 5 "NVQ4/NVQ5/Degree or equiv" 9999 "Foreign/Other"
label define vocc 0 "Not applicable" 1 "Unskilled manual" 2 "Semi-skilled manual" 3 "Skilled manual" 4 "Skilled non-manual" 5 "Managerial technical"  ///
                  6 "Professional" 9999 "Other/not fully described"	
label define vhh_size_cat 1 "1" 2 "2" 3 "3" 4 "4" 5 "5" 6 "6+"
label define vownhome2 0 "No" 1 "Mortgage/shared ownership" 2 "Own it outright"
label define vbmicat 1 "Underweight" 2 "Healthy weight" 3 "Overweight" 4 "Obesity I" 5 "Obesity II" 6 "Obesity III"
label define vemp 0 "Unemployed" 1 "Employed"

label define vagecat1 1 "16-19" 2 "20-24" 3 "25-29" 4 "30-34" 5 "35-39" 6 "40-44" 7 "45-49" 8 "50-54" 9 "55-59" 10 "60-64" 11 "65-69" 12 "70-74"  ///
                      13 "75-79" 14 "80+" 

label define vagecat2 1 "16-19" 2 "20-29" 3 "30-39" 4 "40-49" 5 "50-59" 6 "60-69" 7 "70-79" 8 "80+" 

label define vmarstatus 1 "Married/living with partner" 2 "Widowed/divorced/ separated" 3 "Never married/single"

label define vhh_imd_quint 1 "Quintile 1 [Least deprived]" 2 "Quintile 2" 3 "Quintile 3" 4 "Quintile 4" 5 "Quintile 5 [Most deprived]"

label define vhh_income_quint 1 "I (Lowest)" 2 "II" 3 "III" 4 "IV" 5 "V (Highest)"

label define vbenefit -9 "No answer/refused" -8 "Don't know" -1 "Item not applicable" 0 "No" 1 "Yes"

label define valcmax 0 "None" 1 " <=4 units/day (men), <=3 units/day (women)" 2 ">4 and <=8 units/day (men), >3 and <=6 units/day (women)"   ///
                     3 ">8 units/day (men), >6 units/day (women)"

******************************************************************************************************************************************************
* CLEAN, SUBSET, LABEL & RECODE                                                                                                                      * 
******************************************************************************************************************************************************
				 
drop if year==2005 & samptype==2
drop samptype

*Demographics
 
 *Sex
 label val sex vsex
 
*Age
gen age2_2015est=age
replace age2_2015est=16.5 if Age16g5==1
replace age2_2015est=18.5 if Age16g5==2
replace age2_2015est=22 if Age16g5==3
replace age2_2015est=27 if Age16g5==4
replace age2_2015est=32 if Age16g5==5
replace age2_2015est=37 if Age16g5==6
replace age2_2015est=42 if Age16g5==7
replace age2_2015est=47 if Age16g5==8
replace age2_2015est=52 if Age16g5==9
replace age2_2015est=57 if Age16g5==10
replace age2_2015est=62 if Age16g5==11
replace age2_2015est=67 if Age16g5==12
replace age2_2015est=72 if Age16g5==13
replace age2_2015est=77 if Age16g5==14
replace age2_2015est=82 if Age16g5==15
replace age2_2015est=87 if Age16g5==16
replace age2_2015est=92 if Age16g5==17
replace age2_2015est=92 if year==2014 & age==90
label var age2_2015est "Age in years, with rough estimates for 2015+"

*Using ages from HSE 2015 special licence data
gen age2=age2_2015est
replace age2=Age90 if year==2015
replace age2=92 if Age90==90
label var age2 "Age in years, with rough estimates for 2016+"

 recode age2 (16/19=1) (20/24=2) (25/29=3) (30/34=4) (35/39=5) (40/44=6) (45/49=7) (50/54=8) (55/59=9) (60/64=10)(65/69=11) (70/74=12)  ///
             (75/79=13), gen(agecat1)
 replace agecat1=14 if age2>=80
 replace agecat1=. if age2==.
 label var agecat1 "Age categories (5-year bands)"
 label val agecat1 vagecat1
 
  recode age2 (16/19=1) (20/29=2) (30/39=3) (40/49=4) (50/59=5) (60/69=6) (70/79=7), gen(agecat2)
 replace agecat2=8 if age2>=80
 replace agecat2=. if age2==.
 label var agecat2 "Age categories (10-year bands)"
 label val agecat2 vagecat2
 
 drop Age16g5 Age90 age

*Ethnicity
recode origin5 (1=3) (2=1) (3=4) (4=2) (-9/-2=.) (5=9999), gen(race)
label val race vrace_e
label var race "Ethnicity"
rename race race_e
drop origin5

recode race_e (1=2) (3=1) (4=3) (2=4) (9999=4), gen(race_e2)
label var race_e2 "Ethnicity, 4 categories"
label values race_e2 vrace_e2

label define vrace_eb 1 "White" 2 "BAME/Mixed/Other"
recode race_e (1=2) (3=1) (4=2) (2=2) (9999=2), gen(race_eb)
label var race_eb "Ethnicity, 2 categories"
label values race_eb vrace_eb


*Marital status
recode mstatus_h (1=3) (2=1) (3/5=2) (9=1), gen(marstatus)
replace marstatus=. if marstatus==-2
label val marstatus vmarstatus
label var marstatus "Marital status"
drop mstatus_h


codebook urban_b 
recode urban_b (0=2) (1=1) (-2=.) (.=.), gen(geotype2)
label val geotype2 vgeotype2
label var geotype2 "Geotype - urban/non-urban"
drop urban_b

rename gor geolevel1
replace geolevel1=. if geolevel1==-2
label var geolevel1 "Government Office Region"

*Generating  unique pids

 bysort year: gen id2=_n
generate id2_s = string(id2)
 
 gen year_s = string(year)
 label var year_s "String version of year"
 
gen pid = year_s + id2_s
destring pid, replace
 label var pid "Individual unique identifier"

*Time
gen time=year-1998
label var time "Years since 1998"

rename mintb intm
label var intm "Month of interview"
gen inty=year 
label var inty "Year of interview"

destring point1, replace
rename point1 psu
label var psu "Primary Sampling Unit"

gen stratum=geolevel1
label var stratum "Stratum - trend analysis"

rename qrtint intq

rename vismon vism
label var vism "Month of nurse visit"
replace vism=. if vism<0

rename qrtnvis visq
replace visq=. if visq<0

rename wt_int aweight_int
label var aweight_int "Sampling weight - Adult interview"

replace wt_nurse=. if wt_nurse==0
rename wt_nurse aweight_nonlab
label var aweight_nonlab "Sampling weight - Adult non-lab risk score"

replace wt_blood=. if wt_blood==0
replace wt_blood=wt_bldEL if year==2005 & age_h>64
rename wt_blood aweight_lab
label var aweight_lab "Sampling weight - Adult lab risk score"

gen aweight_int_cvd=.
replace aweight_int_cvd=aweight_int
replace aweight_int_cvd=wt_int_s1 if year==2006
replace aweight_int_cvd=. if aweight_int_cvd==0
label var aweight_int_cvd "Sampling weight - Adult interview, CVD analysis" 

gen aweight_nonlab_cvd=.
replace aweight_nonlab_cvd=aweight_nonlab
replace aweight_nonlab_cvd=wt_nurse_s1 if year==2006
replace aweight_nonlab_cvd=. if aweight_nonlab_cvd==0
label var aweight_nonlab_cvd "Sampling weight - Adult non-lab risk score, CVD analysis" 

gen aweight_lab_cvd=.
replace aweight_lab_cvd=aweight_lab
replace aweight_lab_cvd=wt_blood_s1 if year==2006
replace aweight_lab_cvd=. if aweight_lab_cvd==0
label var aweight_lab_cvd "Sampling weight - Adult lab risk score, CVD analysis" 

*Socioeconomic status

*Home ownership
recode tenureb (-9/-8=.) (1/3=1) (4/6=0), gen(hh_ownhome)
label var hh_ownhome "Home owned by a household member"
label values hh_ownhome vyesno
*Mortgage, own outright & shared ownership have been considered as ownership
*Renting, living rent free or squatting have been considered as not owning.

recode tenureb (-9/-8=.) (1=2) (2/3=1) (4/6=0), gen(hh_ownhome2)
label var hh_ownhome2 "Home owned by a household member, detailed"
label values hh_ownhome2 vownhome2
drop tenureb

*Assets
*codebook car
recode car (-9/-8=.) (1=1) (2=0), gen(hh_ass_car_truck)
label values hh_ass_car_truck vyesno
label var hh_ass_car_truck "Assets: car, truck"
*Those with item not applicable were coded as having no car/truck

recode numcars (-9/-8=.) (-1=.), gen(hh_carnum)
label define vcarnum 0 "None" 1 "One" 2 "Two" 3 "Three or more"
label values hh_carnum vcarnum
label var hh_carnum "Number of cars available"

gen hh_size=hhsize
replace hh_size=. if year==2015 | year==2016 | year==2017
label var hh_size

recode hhsize (1=1) (2=2) (3=3) (4=4) (5=5) (6/12=6), gen(hh_size_cat)
label variable hh_size_cat "Household size (categories)"
label values hh_size_cat vhh_size_cat

*Education
codebook topqual3
sum age2 if topqual3==-1
recode topqual3 (-9/-8=.) (-1=0) (5=1) (4=2) (3=3) (2=4) (1=5) (6=9999) (7=0), gen(edu_e)
label var edu_e "Educational qualifications, England"
label values edu_e vedu_e
*Those with 'Item not applicable' were coded as having No qualification--assuming they are still in school

*Occupation

gen occupation=.
*replace occupation=. if sclass12==-9 | sclass12==-8 
replace occupation=9999 if sclass==8 | sclass12==0 | sclass==7 | sclass12==6 
replace occupation=0 if sclass==-1 | sclass12==-1
replace occupation=1 if sclass==6 | sclass12==5
replace occupation=2 if sclass==5 | sclass12==4
replace occupation=3 if sclass==4 | sclass12==3.2
replace occupation=4 if sclass==3 | sclass12==3.1
replace occupation=5 if sclass==2 | sclass12==2
replace occupation=6 if sclass==1 | sclass12==1
*Unsure if 6 in sclass12 corresponds to armed forces but assuming so. I have grouped them all with other*
*Assumed 3.1 was skilled non-manual and 3.2 was skilled manual following the coding order of sclass*
label variable occupation "Social class/occupation"
label values occupation vocc

recode activb (-99/-1 =.) (1=0) (2/4=1) (5/95=0), gen(emp)

replace emp=. if Activb2==-9 | Activb2==-8 | Activb2==-1
replace emp=0 if Activb2== 1
replace emp=1 if Activb2==2 | Activb2==3

replace emp=0 if Activb2==4 | Activb2==5 | Activb2==6 | Activb2==7 | Activb2==8 | Activb2==9 | Activb2==95
label var emp "Employment status"
label val emp vemp

/*
sclass                                                         Social Class
---------------------------------------------------------------------------

                  Type: Numeric (byte)
                 Label: sclass

                 Range: [-1,8]                        Units: 1
         Unique values: 9                         Missing .: 66,234/145,375

            Tabulation: Freq.   Numeric  Label
                        4,207        -1  Item not applicable
                        3,688         1  I    - Professional
                       22,072         2  II   - Managerial technical
                       17,891         3  IIIN - Skilled non-manual
                       13,442         4  IIIM - Skilled manual
                       13,187         5  IV   - Semi-skilled manual
                        4,299         6  V    - Unskilled manual
                          162         7  Armed forces
                          193         8  Not fully described
                       66,234         .  

					   
		sclass 12:
		
    Registrar General's Social |
      Class of individual (old |
  scheme, using SOC2000 instea |      Freq.     Percent        Cum.
-------------------------------+-----------------------------------
       -9      No answer/refused |        231        0.47        0.47
	   -8	 Don't know
       -1    Item not applicable |      3,118        6.34        6.81
0 Not classifiable from SOC2000 |         33        0.07        6.87
1              I - Professional |      2,759        5.61       12.48
2     II - Managerial technical |     15,170       30.83       43.31
                           3.1 |      9,852       20.02       63.33
                           3.2 |      7,611       15.47       78.80
4      IV - Semi-skilled manual |      8,178       16.62       95.42
5          V - Unskilled manual |      2,217        4.51       99.93
                             6 |    

*/

/*gen hh_imd_quint=.
replace hh_imd_quint=1 if qimd==1 | imd2004==1 | imd2007==1
replace hh_imd_quint=2 if qimd==2 | imd2004==2 | imd2007==2
replace hh_imd_quint=3 if qimd==3 | imd2004==3 | imd2007==3
replace hh_imd_quint=4 if qimd==4 | imd2004==4 | imd2007==4
replace hh_imd_quint=5 if qimd==5 | imd2004==5 | imd2007==5
label define vhh_imd_quint 1 "Quintile 1 [Least deprived]" 2 "Quintile 2" 3 "Quintile 3" 4 "Quintile 4" 5 "Quintile 5 [Most deprived]"
label values hh_imd_quint vhh_imd_quint
label var hh_imd_quint "Neighbourhood Index of Multiple Deprivation Quintiles [relative]"
tab hh_imd_quint
drop qimd imd2004 imd2007
*/

rename imd_h hh_imd_quint
label values hh_imd_quint vhh_imd_quint
label var hh_imd_quint "Neighbourhood Index of Multiple Deprivation Quintiles [relative]"

gen hh_income=.
replace hh_income=450 if totinc==1
replace hh_income=1060 if totinc==2
replace hh_income=2100 if totinc==3
replace hh_income=3100 if totinc==4
replace hh_income=4400 if totinc==5
replace hh_income=6500 if totinc==6
replace hh_income=9100 if totinc==7
replace hh_income=11700 if totinc==8
replace hh_income=14300 if totinc==9
replace hh_income=16900 if totinc==10
replace hh_income=19500 if totinc==11
replace hh_income=22100 if totinc==12
replace hh_income=24700 if totinc==13
replace hh_income=27300 if totinc==14
replace hh_income=29900 if totinc==15
replace hh_income=32500 if totinc==16
replace hh_income=35100 if totinc==17
replace hh_income=39000 if totinc==18
replace hh_income=44200 if totinc==19
replace hh_income=49400 if totinc==20
replace hh_income=56000 if totinc==21
replace hh_income=65000 if totinc==22
replace hh_income=75000 if totinc==23
replace hh_income=85000 if totinc==24
replace hh_income=95000 if totinc==25
replace hh_income=105000 if totinc==26
replace hh_income=115000 if totinc==27
replace hh_income=125000 if totinc==28
replace hh_income=135000 if totinc==29
replace hh_income=145000 if totinc==30
replace hh_income=160000 if totinc==31
label variable hh_income "Household income"

rename eqv5 hh_income_quint
replace hh_income_quint=. if hh_income_quint<0
label values hh_income_quint vhh_income_quint
label var hh_income_quint "Household income quintiles"

rename eqvinc hh_income_eq
replace hh_income_eq=. if hh_income_eq<0
label var hh_income_eq "Household income, equivalised"

*Public benefits

label values benefit* vbenefit

recode benefit* (-9/-1=.)
egen hh_recgrant=rowmax(benefit*)
label values hh_recgrant vyesno
label var hh_recgrant "Household receives government benefits"
*Missing values will not be counted if the rest of the benefits are 0, only if missing on all of them. Based on checks below, 
*there do not seem to be any missing cases where it is not missing on all of them.

drop benefit*

*Pregnancy status

recode pregntj (-9=.) (-8=0) (-2=.) (-1=0) (2=0) (1=1), gen(currpreg)
replace currpreg=. if sex==1
replace currpreg=0 if sex==2 & age2>49 & age2!=.
label values currpreg vcurrpreg
label variable currpreg "Current pregnancy status (women only)"

drop pregntj pregnowb

**MEASUREMENTS**

 replace sys1om=. if sys1om<60
 replace sys1om=. if sys1om>270
 gen sbp1=sys1om
 label var sbp1 "Systolic Blood Pressure [mmHg] - reading 1"
 
 replace sys2om=. if sys2om<60
 replace sys2om=. if sys2om>270
 gen sbp2=sys2om
 label var sbp2 "Systolic Blood Pressure [mmHg] - reading 2"

 replace sys3om=. if sys3om<60
 replace sys3om=. if sys3om>270
 gen sbp3=sys3om
 label var sbp3 "Systolic Blood Pressure [mmHg] - reading 3"
 
 replace dias1om=. if dias1om<60
 replace dias1om=. if dias1om>270
 gen dbp1=dias1om
 label var dbp1 "Diastolic Blood Pressure [mmHg] - reading 1"
 
 replace dias2om=. if dias2om<60
 replace dias2om=. if dias2om>270
 gen dbp2=dias2om
 label var dbp2 "Diastolic Blood Pressure [mmHg] - reading 2"
 
 replace dias3om=. if dias3om<60
 replace dias3om=. if dias3om>270
 gen dbp3=dias3om
 label var dbp3 "Diastolic Blood Pressure [mmHg] - reading 3"

replace sbp1=. if sys1om-dias1om<15
replace dbp1=. if sys1om-dias1om<15

replace sbp2=. if sys2om-dias2om<15
replace dbp2=. if sys2om-dias2om<15

replace sbp3=. if sys3om-dias3om<15
replace dbp3=. if sys3om-dias3om<15
 
 egen sbp_mean = rowmean(sbp2 sbp3)
 label var sbp_mean "Mean Systolic Blood Pressure [mmHg], 2nd & 3rd reading"
 rename sbp_mean sbp_mean2
 
 egen dbp_mean = rowmean(dbp2 dbp3)
 label var dbp_mean "Mean Diastolic Blood Pressure [mmHg], 2nd & 3rd reading"
 rename dbp_mean dbp_mean2

  egen sbp = rowmean(sbp1 sbp2 sbp3)
 label var sbp "Average of available Systolic Blood Pressure readings [mmHg]"
 rename sbp sbp_mean1
 
 egen dbp = rowmean(dbp1 dbp2 dbp3)
 label var dbp "Average of available Diastolic Blood Pressure readings [mmHg]"
 rename dbp dbp_mean1
 
 *Resting heart rate
 replace pulse1=. if pulse1<0
 replace pulse2=. if pulse2<0
 replace pulse3=. if pulse3<0
 
 replace puls1om=. if puls1om<0
 replace puls2om=. if puls2om<0
 replace puls3om=. if puls3om<0
 
 sum pulse1 pulse2 pulse3 puls1om puls2om puls3om

 gen rhr1=puls1om
 replace rhr1=pulse1 if year<2003
 replace rhr1=. if rhr1<20| rhr1>250  /*implausible*/
label var rhr1 "Resting Heart Rate [bpm] - reading 1"

gen rhr2=puls2om
 replace rhr2=pulse2 if year<2003
 replace rhr2=. if rhr2<20| rhr2>250  /*implausible*/
label var rhr2 "Resting Heart Rate [bpm] - reading 2"

gen rhr3=puls3om
 replace rhr3=pulse3 if year<2003
 replace rhr3=. if rhr3<20| rhr3>250  /*implausible*/
label var rhr3 "Resting Heart Rate [bpm] - reading 3"

egen rhr_mean1 = rowmean(rhr1 rhr2 rhr3)
label var rhr_mean1 "Average of available resting heart rate readings [bpm]"

egen rhr_mean2 = rowmean(rhr2 rhr3)
label var rhr_mean2 "Mean resting heart rate [bpm], 2nd & 3rd reading"

drop puls*
 
 *BMI
 rename htval height
replace height=. if height<120 | height>220
label var height "Height [cm] - Average of available readings"

rename wtval2 weight
replace weight=. if weight<25 & sex==2 /*implausible women*/
replace weight=. if weight<35 & sex==1 /*implausible men*/
replace weight=. if weight>250 /*implausible*/
label var weight "Weight [kg] - Average of available readings"

gen bmi=weight/(height/100)^2
replace bmi=. if bmi<10 | bmi>131
label var bmi "Body Mass Index [kg/m2], calculated"

egen bmicat = cut(bmi) , at(10, 18.5, 25, 30, 35, 40, 131)
recode bmicat (10=1) (18.5=2) (25=3) (30=4) (35=5) (40=6)
label values bmicat vbmicat
label var bmicat "BMI categories"


*Waist circumference*
replace waist1=. if waist1<30 | waist1>220 /*implausible*/
replace waist2=. if waist2<30 | waist2>220 /*implausible*/
replace waist3=. if waist3<30 | waist3>220 /*implausible*/

label var waist1 "Waist circumference [cm] - reading 1"
label var waist2 "Waist circumference [cm] - reading 2"
label var waist3 "Waist circumference [cm] - reading 3"

egen waist = rowmean(waist1 waist2 waist3)
label var waist "Waist circumference [cm] - Average of available readings"


*Hip circumference*
replace hip1=. if hip1<0 | hip1==999
replace hip1=. if hip1<40 | hip1>230 /*implausible*/

replace hip2=. if hip2<0 | hip2==999
replace hip2=. if hip2<40 | hip2>230 /*implausible*/

replace hip3=. if hip3<0 | hip3==999
replace hip3=. if hip3<40 | hip3>230 /*implausible*/

label var hip1 "Hip circumference [cm] - reading 1"
label var hip2 "Hip circumference [cm] - reading 2"
label var hip3 "Hip circumference [cm] - reading 3"

egen hip = rowmean(hip1 hip2 hip3)
label var hip "Hip circumference [cm] - Average of available readings"

*Cholesterol

gen chol_tot=cholval13
replace chol_tot=. if cholval13<0
replace chol_tot=. if cholval13<1.75 | cholval13>20.00 
label var chol_tot "Total cholesterol [mmol/l]"
sum chol_tot
*  (D) Valid Total Cholesterol Result mmol/L (incl those on lipid-lowering drugs) (sample received a...
drop cholval13

gen chol_hdl=hdlval1a
replace chol_hdl=. if hdlval1a<0
replace chol_hdl=. if hdlval1a<0.40 | hdlval1a>5.00 
label var chol_hdl "High-density lipoprotein (HDL) cholesterol [mmol/l]"
sum chol_hdl
drop hdlval1a

replace chol_hdl=. if chol_tot<chol_hdl & chol_hdl !=.
replace chol_tot=. if chol_tot<chol_hdl & chol_hdl !=.
replace chol_hdl=. if chol_hdl>chol_tot & chol_hdl !=.
replace chol_tot=. if chol_hdl>chol_tot & chol_hdl !=.

*HbA1c
replace glyhb_h=. if glyhb_h<2.5 | glyhb_h>25
gen hbA1c=10.93*glyhb_h-23.5 /*Converting percent to mmol/mol for pre 2012*/
label var hbA1c "Hb1Ac [mmol/mol]"

replace glyhb2_h=. if glyhb2_h<0
drop iffcvala IFCCA1 glyhbval glyhbvala GlyHb
  
*Diagnosis of conditions

*Diabetes
 gen diag_diab2=.
 replace diag_diab2=1 if diabete2==1
 replace diag_diab2=0 if diabete2==2
 replace diag_diab2=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
 label var diag_diab2 "Diagnosis: diabetes, excluding pregnancy"
label val diag_diab2 vyesno
 drop diabete2
 
gen diag_diab=.
replace diag_diab=1 if diabetes==1 | docinfo1==1
replace diag_diab=0 if diabetes==2 | docinfo1==2 | everdi==2 /*Don't know and refusal for ever had are listed as missing here.*/
*replace diag_diab=0 if diabetes==2 | docinfo1==2 | diabetes==-1 | docinfo1==-1 /*Don't know and refusal here are coded as no, 
*following HSE recoding for diabete2*/
replace diag_diab=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_diab "Diagnosis: diabetes"
label val diag_diab vyesno

 
* Hypertension 
recode bp1 (-9/-1=.) (.=.) (1=1) (2=0), gen(diag_hbp2)
replace diag_hbp2=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_hbp2 "Diagnosis: hypertension, excluding pregnancy"
label val diag_hbp2 vyesno
 
gen diag_hbp=.
replace diag_hbp=1 if docnurbp==1 | docbp==1
replace diag_hbp=0 if docnurbp==2 | docbp==2 | everbp==2 /*Don't know and refusal for ever had are listed as missing here.*/
*replace diag_hbp=0 if docnurbp==2 | docbp==2 | docnurbp==-1 | docbp==-1 /*Don't know and refusal for ever had are coded as no, 
*following HSE recoding for bp1*/
replace diag_hbp=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_hbp "Diagnosis: hypertension"
label val diag_hbp vyesno

* Angina
gen diag_angi=.
replace diag_angi=1 if angidef==1
replace diag_angi=0 if docangi==2 | everangi==2 /*Don't know and refusal for ever had are listed as missing here.*/
*replace diag_angi=0 if angidef==2 /*Don't know and refusal for ever had are coded as no, following HSE recoding for angidef. 
*Alternatively diag_angi=0 if docangi==2 | docangi==-1*/
replace diag_angi=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_angi "Diagnosis: angina"
label val diag_angi vyesno

*Heart attack
gen diag_mi=.
replace diag_mi=1 if heartdef==1
replace diag_mi=0 if docheart==2 | everhart==2 /*Don't know and refusal for ever had are listed as missing here.*/
*replace diag_mi=0 if heartdef==2 /*Don't know and refusal for ever had are coded as no, following HSE recoding for midef. 
*Alternatively diag_mi=0 if docheart==2 | docheart==-1*/
replace diag_mi=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_mi "Diagnosis: heart attack (myocardial infarction)"
label val diag_mi vyesno

*Ischaemic heart disease
gen diag_isch=.
replace diag_isch=1 if diag_mi==1 | diag_angi==1
replace diag_isch=0 if diag_mi==0 & diag_angi==0
label var diag_isch "Diagnosis: heart attack/angina"
label val diag_isch vyesno

*Stroke
gen diag_stroke=.
replace diag_stroke=1 if strodef==1
replace diag_stroke=0 if docstro==2 | everstro==2 /*Don't know and refusal for ever had are listed as missing here.*/
*replace diag_stroke=0 if strodef==2 /*Don't know and refusal for ever had are coded as no, following HSE recoding for strokedef. 
*Alternatively, diag_stroke=0 if docstro==2 | docstro==-1*/
replace diag_stroke=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_stroke "Diagnosis: stroke"
label val diag_stroke vyesno

 *Other heart condition
gen diag_heart_ni=.
replace diag_heart_ni=1 if ohtdef==1
replace diag_heart_ni=1 if murdoc==1
replace diag_heart_ni=1 if iregdef==1
replace diag_heart_ni=0 if (docoht==2 | everoht==2) & (murdoc==2 | evermur==2) & (docireg==2 | everireg==2) /*Don't know/refusal coded as missing*/
*replace diag_heart_ni=0 if ohtdef==2 & (murdoc==2 | murdoc==-1) & iregdef==2 
*Don't know and refusal for ever had are coded as no, following HSE recoding*
replace diag_heart_ni=. if year==2005 & age2<65 /*Those under age 65 were not asked CVD questions in 2005 */
label var diag_heart_ni "Diagnosis: other heart condition/murmur/abormal heart rate"
label val diag_heart_ni vyesno

*Conditions: Not indicated whether diagnosed. Should we rename them by removing the 'diag_' prefix? 

gen cancer=.
replace cancer=0 if compm1==0 | complst1==0
replace cancer=1 if compm1==1 | complst1==1
label val cancer vyesno
label var cancer "Cancer"
rename cancer diag_cancer

gen heart=.
replace heart=0 if compm7==0 | complst7==0
replace heart=1 if compm7==1 | complst7==1
label val heart vyesno
label var heart "Heart and circulatory condition"
rename heart diag_heart 


gen lung=.
replace lung=0 if compm8==0 | complst8==0
replace lung=1 if compm8==1 | complst8==1
label val lung vyesno
label var lung "Respiratory condition"
rename lung diag_lung

*Asthma
gen diag_asth=0
replace diag_asth=. if illsm1==-9 | illsm2==-9 | illsm3==-9 | illsm4==-9 | illsm5==-9 | illsm6==-9
replace diag_asth=. if illsm1==99 | illsm2==99 | illsm3==99 | illsm4==99 | illsm5==99 | illsm6==99
replace diag_asth=. if ill12m1==-9 | ill12m2==-9 | ill12m3==-9 | ill12m4==-9 | ill12m5==-9 | ill12m6==-9
replace diag_asth=. if longill<0 | ILL12m<0
replace diag_asth=1 if illsm1==23 | illsm2==23 | illsm3==23 | illsm4==23 | illsm5==23 | illsm6==23  
replace diag_asth=1 if ill12m1==23 | ill12m2==23 | ill12m3==23 | ill12m4==23 | ill12m5==23 | ill12m6==23
*replace diag_asth=0 if longill==2 | ILL12m==2
replace diag_asth=. if year>2015
label val diag_asth vyesno
label var diag_asth "Asthma diagnosis"

*Chronic lung disease
gen diag_emph=0
replace diag_emph=. if illsm1==-9 | illsm2==-9 | illsm3==-9 | illsm4==-9 | illsm5==-9 | illsm6==-9
replace diag_emph=. if illsm1==99 | illsm2==99 | illsm3==99 | illsm4==99 | illsm5==99 | illsm6==99
replace diag_emph=. if ill12m1==-9 | ill12m2==-9 | ill12m3==-9 | ill12m4==-9 | ill12m5==-9 | ill12m6==-9
replace diag_emph=. if longill<0 | ILL12m<0
replace diag_emph=1 if illsm1==22 | illsm2==22 | illsm3==22 | illsm4==22 | illsm5==22 | illsm6==22  
replace diag_emph=1 if ill12m1==22 | ill12m2==22 | ill12m3==22 | ill12m4==22 | ill12m5==22 | ill12m6==22
*replace diag_emph=0 if longill==2 | ILL12m==2
replace diag_emph=. if year>2015
label val diag_emph vyesno
label var diag_emph "Emphysema/bronchitis diagnosis"


gen mental=.
replace mental=0 if compm3==0 | complst3==0
replace mental=1 if compm3==1 | complst3==1
label val mental vyesno
label var mental "Mental disorder"
rename mental diag_mental

gen infectious=.
replace infectious=0 if compm13==0 | complst13==0
replace infectious=1 if compm13==1 | complst13==1
label val infectious vyesno
label var infectious "Infectious or parasitic disorder"
rename infectious diag_infectious

gen metabolic=.
replace metabolic=0 if compm2==0 | complst2==0
replace metabolic=1 if compm2==1 | complst2==1
label val metabolic vyesno
label var metabolic "Endocrine/metabolic disorder"
rename metabolic diag_metabolic

gen nerve=.
replace nerve=0 if compm4==0 | complst4==0
replace nerve=1 if compm4==1 | complst4==1
label val nerve vyesno
label var nerve "Nervous system disorder"
rename nerve diag_nerve

gen blood=.
replace blood=0 if compm14==0 | complst14==0
replace blood=1 if compm14==1 | complst14==1
label val blood vyesno
label var blood "Blood disorder"
rename blood diag_blood


*Self-perceived health
 recode genhelf (-9/-8=.) (4/5=1) (3=2) (2=3) (2=3) (1=4), gen(self_health)
 *label val self_health vself_health_e
 label val self_health vself_health
 
 label var self_health "Self-perception of health status"
 drop genhelf
 
 *Antihypertensives
 /*
 tab BPmed
 rename BPmed bpmed_coded2
 replace bpmed_coded2=. if bpmed_coded2<0
 label var bpmed_coded2 "Prescribed antihypertensive medication - self"
 label val bpmed_coded2 vyesno
 tab bpmed_coded2
 /*Those with no answer/refused were coded as not taking medication*/
 *This is based on the nurse visit
 */
 
gen bpmed=.
replace bpmed=1 if medbp==1 | medcinbp==1
replace bpmed=0 if medbp==2 | medcinbp==2
replace bpmed=0 if medbp==-1 | medcinbp==-1 /*This excludes those diagnosed other than during pregnancy (?)*/
replace bpmed=. if year==2005 & age_h<65 /*Those under age 65 were not asked CVD questions in 2005 */
replace bpmed=. if wt_int_s1==0 /*Non CVD sample*/
label var bpmed "Current use of antihypertensive medication - self"
label val bpmed vyesno
/*This is based on the regular interview and not the nurse visit*/

gen bpmed_coded=.
replace bpmed_coded=0 if bpmedd==0 | bpmedd2==0
replace bpmed_coded=1 if bpmedd==1 | bpmedd2==1
replace bpmed_coded=0 if bpmedd==-1 & medcnjd==2 /*Not taking any prescribed medication*/
replace bpmed_coded=0 if bpmedd2==-1 & medcnjd==2 /*Not taking any prescribed medication*/
replace bpmed_coded=. if medcnjd==-1 /*No nurse visit*/
replace bpmed_coded=. if bpmedd==-1 & medcnjd==3 /*Unable to code medication*/
label var bpmed_coded "Current use of antihypertensive medication - coded"
label val bpmed_coded vyesno
*Those with no answer/refused coded as missing here*/


*Diabetes medication

recode dimed_h (-8=.) (-1=0) (2=0) (1=1), gen(diabmed)
label values diabmed vyesno
replace diabmed=. if year==2006 & wt_int_s1==0
label var diabmed "Diabetes medication - Self report"

*Cardiovascular medication*/
gen heartmed=.
replace heartmed=1 if medtyp1_h==1
replace heartmed=0 if medtyp1_h==0
label variable heartmed "Cardiovascular medication - Self report" 
label values heartmed vyesno

recode medheart (-9/-8=.) (-1=0) (1=1) (2=0), gen (heartmed2)
replace heartmed2=. if year==2006 & wt_int_s1==0
label variable heartmed2 "Heart condition or stroke medication - Self report"
label values heartmed2 vyesno

*Lipid lowering drugs
gen cholmed=.
replace cholmed=1 if lipid==1
replace cholmed=0 if lipid==0
label variable cholmed "Lipid-lowering medication - Self report" 
label values cholmed vyesno

*Contraceptives

recode pilluse (-9/-2=.)(-1=0)(1=1) (2=0), gen (contraceptives)
replace contraceptives=1 if medtyp14==1
replace contraceptives=0 if medtyp14==0
replace contraceptives=. if medtyp14==-1
replace contraceptives=. if sex==1
label values contraceptives vyesno
label var contraceptives "Currently using contraceptives (women only)"


drop medtyp1_h lipid dimed_h
drop pilluse medtyp14 pillevr

*Mental health

*GHQ-12
replace ghq12scr=. if ghq12scr<0

*Healthcare utilisation

gen hcare1mo=.
replace hcare1mo=1 if ndoctalk==1
replace hcare1mo=1 if whendoc==1
replace hcare1mo=1 if whendoc==2 & year==2005
replace hcare1mo=0 if year!=2005 & (whendoc==2|whendoc==3|whendoc==4| whendoc==5|whendoc==6)
replace hcare1mo=0 if year==2005 & (whendoc==3|whendoc==4|whendoc==5|whendoc==6 |whendoc==7)
*From 1998-2005, ndoctalk included those who consulted a doctor even if not on their own behalf. In 2005 whendoc included those who consulted 
*a doctor in the last 2 weeks. These have been checked and are accounted for in this variable. 

label values hcare1mo vyesno
label variable hcare1mo "Health consultation in the last month"

gen hcare12mo=.
replace hcare12mo=1 if ndoctalk==1
replace hcare12mo=1 if year!=2005 & (whendoc==1|whendoc==2|whendoc==3|whendoc==4)
replace hcare12mo=1 if year==2005 &(whendoc==1|whendoc==2|whendoc==3|whendoc==4|whendoc==5) 
replace hcare12mo=0 if year!=2005 & (whendoc==5|whendoc==6)
replace hcare12mo=0 if year==2005 & (whendoc==6 |whendoc==7)
/*From 1998-2005, ndoctalk included those who consulted a doctor even if not on their own behalf. In 2005 whendoc included those who consulted a 
doctor in the last 2 weeks. These have been checked and are accounted for in this variable. */
label values hcare12mo vyesno
label variable hcare12mo "Health consultation in the last year"

*Inpatient visits

gen ihcare12mo=.
replace ihcare12mo=1 if inpat==1 
replace ihcare12mo=0 if inpat==2
label values ihcare12mo vyesno
label variable ihcare12mo "Inpatient visits in the last year"


drop whendoc ndoctalk outpat Outpat01 OutPat02 OutPat03 OutPat95 inpat

*Physical activity
 recode a30t06c (-9/-1=.), gen (exercisefreq_e)
 label define vexercisefreq_e 0 "Never" 1 "Less than once per week" 2 "Once or twice per week" 3 "Three of four times per week"   ///
                              4 "Five or more times per week"
 label values exercisefreq_e vexercisefreq_e
 label var exercisefreq_e "Weekly frequency of 30+ min moderate or more activity"


*Alcohol
label define valcstatus 0 "Never drinker" 1 "Former drinker" 2 "Current drinker"
label define vcurralc 0 "Non-drinker" 1 "Current drinker"

gen curralc=.
replace curralc=1 if dnnow==1
replace curralc=1 if dnnow==2 & dnany==1
replace curralc=0 if dnany==2 
label values curralc vcurralc
label variable curralc "Current drinking"

gen alcstatus=.
replace alcstatus=2 if dnnow==1
replace alcstatus=2 if dnnow==2 & dnany==1
replace alcstatus=1 if dnany==2 & dnevr==2
replace alcstatus=0 if dnany==2 & dnevr==1
label var alcstatus "Alcohol use status"
label values alcstatus valcstatus

*"Item not applicable" here are coded as missing. Most are below age 25, except in 1998 & 2011.

gen alcmax=alclimit07b
replace alcmax=. if alclimit07b<0
label var alcmax "Alcohol units consumed on heaviest day in the week"
label values alcmax valcmax
sum alcmax
drop alclimit07b


*Fruit & veggie consumption

recode porftvg (-9/-1=.) (0/5=0) (6/9=1), gen(fruitveg)
label values fruitveg vyesno
label var fruitveg "5 or more portions of fruits/vegetables yesterday"

 *Smoking
 
 tab cigsta3
 gen currsmok=.
 replace currsmok=1 if cigsta3==1
 replace currsmok=0 if cigsta3==2 | cigsta3==3
 label val currsmok vyesno
 label var currsmok "Current smoking"
 
 recode cigsta3 (3=0) (2=1) (1=2) (-9/-1=.), gen (smokstatus)
 label var smokstatus  "Smoking status"
label val smokstatus vsmokstatus
drop cigsta3

*Environmental
replace airtemp=. if airtemp<0
  
*Dropping unneeded/original variables

drop omsysval highbp bmival2 CVD_Risk sys1om sys2om sys3om dias1om dias2om dias3om
drop medbp medcinbp bp1 ever* comp* topqual3 omdiaval car numcars *val *def doc* /*cluster Cluster_adults*/ diabetes medcnjd
drop hhsize
drop sclass sclass12
drop ill12m* ILL12m illsm* longill limitill limlast totinc
drop a30t06c year_s id2_s bpmedd bpmedd2 dnnow dnany dnevr
drop activb Activb2 econact stwork

*Excluding cases

drop if geolevel1==.

*For restricted data:

drop age2
replace diag_asth=. if year==2015
replace diag_blood=. if year==2015
replace diag_cancer=. if year==2015 
replace diag_emph=. if year==2015
replace diag_infectious=. if year==2015
replace diag_lung=. if year==2015
replace diag_mental=. if year==2015
replace diag_metabolic=. if year==2015
replace diag_heart=. if year==2015
replace diag_nerve=. if year==2015
replace hh_income=. if year==2015
replace hh_income_eq=. if year==2015
 
******************************************************************************************************************************************************
* SUBSETTING, RENAMING, ADDITIONAL DERIVED                                                                                                           * 
******************************************************************************************************************************************************
gen age = round(age_h,1.0)

drop hid wt_int_s1 wt_int_s2 wt_nurse_s1 wt_nurse_s2 wt_blood_s1 wt_blood_s2 age_h hh_imd_quint sclass_h porftvg totalwug totalwu diag_hbp_h  ///
     diag_stroke_h diag_angi_h diag_mi_h diag_diab_h diag_isch_h murmur1 ihdis cvdis murdoc pill3 bpmedd_h bpmedc_h medheart ag16g10 glyhb2_h  ///
	 wt_intEL wt_nurEL wt_bldEL age2_2015est id2 time insulin diabtype age_h

gen source = 12	 
label var source "Source Dataset"
label define vsource 12 "HSE"
label val source vsource
	 
******************************************************************************************************************************************************
* STRIP VALUE LABELS FROM NUMERICAL VARIABLES                                                                                                        * 
******************************************************************************************************************************************************

# delimit ;	 
label values pid psu stratum aweight_int_cvd aweight_nonlab_cvd aweight_lab_cvd aweight_int aweight_nonlab aweight_lab intm vism inty hh_size   
             hh_income hh_income_eq ghq12scr age height weight waist1 waist2 waist3 waist hip1 hip2 hip3 hip sbp1 sbp2 sbp3 sbp_mean2 sbp_mean1  
			 dbp1 dbp2 dbp3 dbp_mean2 dbp_mean1 rhr1 rhr2 rhr3 rhr_mean1 rhr_mean2 airtemp bmi glyhb_h hbA1c chol_tot chol_hdl .
;
# delimit cr
	 
******************************************************************************************************************************************************
* ORDER                                                                                                                                              * 
******************************************************************************************************************************************************
 
# delimit ;	 
order 
source 
year
id pid
psu stratum aweight_int_cvd aweight_nonlab_cvd aweight_lab_cvd aweight_int aweight_nonlab aweight_lab
geolevel1 geotype2
intm vism intq visq inty
hh_size hh_size_cat hh_ownhome hh_ownhome2
hh_recgrant 
hh_ass_car_truck hh_carnum
hh_income hh_income_eq hh_income_quint
ghq12scr
sex age agecat1 agecat2 race_e race_e2 race_eb marstatus edu_e occupation emp
smokstatus currsmok alcstatus curralc alcmax fruitveg exercisefreq_e
self_health diag*
bpmed bpmed_coded diabmed heartmed heartmed2 cholmed contraceptives 
currpreg 
height* weight* waist* hip* sbp* dbp* rhr* airtemp
bmi bmicat
glyhb_h hbA1c chol_tot chol_hdl 
hcare1mo hcare12mo ihcare12mo
;
# delimit cr

******************************************************************************************************************************************************
* SURVEY SETTINGS                                                                                                                                    * 
******************************************************************************************************************************************************

svyset psu [pweight=aweight_int], strata(stratum) singleunit(certainty)
 
******************************************************************************************************************************************************
* SAVE                                                                                                                                               * 
******************************************************************************************************************************************************

* Label the datset
label data "EXPOSE ENGLAND - V. 1.2"

* Save   
save "$OUT/EXPOSE_EN.dta", replace

